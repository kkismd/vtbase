;; Subroutines in this library are ported from
;;   http://wilsonminesco.com/stacks/


;; 8 bytes variable space at zeropage
N       :=$01

zero                            ;; ZERO:                        ; ZERO and
false                           ;; FALSE:                       ; FALSE (to pu the constant -1 on the stack
push_false                      ;; PUSH_FALSE: DEX2             ; use the same code as PUSH_FALSE.
        X=--
set_false                       ;; SET_FALSE:  STZ   1,X        ; NMOS will need to load A or Y with 0 to store.
        A=0 (1+X)=A             ;;             STZ   0,X
        (0+X)=A
        #=!                     ;;             RTS

one     A=1                     ;; ONE:    LDA  #1         ; Put 1 on the data stack.      ( -- 1 )
one_o1  X=--                    ;;  o1$:   DEX2
        (0+X)=A                 ;;         STA  0,X
        A=0 (1+X)=A             ;;         STZ  1,X
        #=!                     ;;         RTS
                                ;;--------------
                                ;; 
two     A=2                     ;; TWO:    LDA  #2         ; Put 2 on the data stack.      ( -- 2 )
        C=0 ;=<,one_o1          ;;         BRA  o1$
                                ;;--------------
                                ;; 
three   A=3                     ;; THREE:  LDA  #3         ; Put 3 on the data stack.      ( -- 3 )
        C=0 ;=<,one_o1          ;;         BRA  o1$
                                ;;--------------
                                ;;         
four    A=4                     ;; FOUR:   LDA  #4
        C=0 ;=<,one_o1          ;;         BRA  o1$


bl      A=$20                   ;; BL:     LDA  #$20       ; Put $20, the blank (space) char, on the data stack.
        X=--                    ;;  bl1$   DEX2
        (0+X)=A                 ;;         STA  0,X
        A=0 (1+X)=A             ;;         STZ  1,X
        #=!                     ;;         RTS

fetch   A=[0+X]                 ;; FETCH:  LDA  (0,X)
        [=A                     ;;         PHA
        (0+X)=+                 ;;         INC  0,X
        ;=\,.1                  ;;         BNE  1$
        (1+X)=+                 ;;         INC  1,X
.1      A=[0+X]                 ;;  1$:    LDA  (0,X)
        C=0 ;=<,put             ;;         BRA  PUT

push    X=--                    ;; PUSH:   DEX2

put     (1+X)=A                 ;; PUT:    STA  1,X
        A=]                     ;;         PLA
        (0+X)=A                 ;;         STA  0,X
        #=!                     ;;         RTS

swap    A=(0+X)                 ;; SWAP:   LDA  0,X        ; Swap the top two data-stack cells.  ( n1 n2 -- n2 n1 )
        Y=(2+X)                 ;;         LDY  2,X
        (2+X)=A                 ;;         STA  2,X
        (0+X)=Y                 ;;         STY  0,X

        A=(1+X)                 ;;         LDA  1,X
        Y=(3+X)                 ;;         LDY  3,X
        (3+X)=A                 ;;         STA  3,X
        (1+X)=Y                 ;;         STY  1,X
        #=!                     ;;         RTS

;; Store n at addr.
;; ( n addr -- )  
store   A=(2+X)                 ;; STORE:  LDA  2,X
        [0+X]=A                 ;;         STA  (0,X)
        (0+X)=+                 ;;         INC  0,X
        ;=\,.st1                ;;         BNE  st1$
        (1+X)=+                 ;;         INC  1,X
.st1    A=(3+X)                 ;;  st1$:  LDA  3,X
.st2    [0+X]=A                 ;;  st2$:  STA  (0,X)
        X=++++                  ;;         POP2
        #=!                     ;;         RTS


rrot    !=rot

;; ROTate the three top data-stack cells, pulling the one farthest down to the top.
;; ( n1 n2 n3 -- n2 n3 n1 )
rot     Y=(0+X)                 ;; ROT:    LDY  0,X          01 23 45
        A=(4+X)                 ;;         LDA  4,X     (1)  n3 n2 n1
        (0+X)=A                 ;;         STA  0,X     (2)  n1 n2 n1   4->0
        A=(2+X)                 ;;         LDA  2,X        
        (4+X)=A                 ;;         STA  4,X     (3)  n1 n2 n2   2->4
        (2+X)=Y                 ;;         STY  2,X     (4)  n1 n3 n2   0->2
                                ;; 
        Y=(1+X)                 ;;         LDY  1,X
        A=(5+X)                 ;;         LDA  5,X     (1)  n3 n2 n1
        (1+X)=A                 ;;         STA  1,X     (2)  n1 n2 n1   5->1
        A=(3+X)                 ;;         LDA  3,X
        (5+X)=A                 ;;         STA  5,X     (3)  n1 n2 n2   3->5
        (3+X)=Y                 ;;         STY  3,X     (4)  n1 n3 n2   1->3
        #=!                     ;;         RTS

;; multiply                     ( n1 n2 -- n1*n2 )
star    A=Y [=A                 ;; STAR:   PHY
        A=0 (N)=A               ;;         STZ  N
        Y=0                     ;;         LDY  #0
.1      >=(3+X)                 ;; sta1$:     LSR  3,X
        )=(2+X)                 ;;            ROR  2,X
        ;=<,.2                  ;;            BCC  sta2$
        C=0                     ;;               CLC
        A=(N)                   ;;               LDA  N
        A=A+(0+X)               ;;               ADC  0,X
        (N)=A                   ;;               STA  N
        A=Y                     ;;               TYA
        A=AC+(1+X)              ;;            ADC  1,X
        Y=A                     ;;            TAY
.2      <=(0+X)                 ;; sta2$:     ASL  0,X
        (=(1+X)                 ;;            ROL  1,X
                                ;; 
        A=(2+X)                 ;;            LDA  2,X
        A=A|(3+X)               ;;            ORA  3,X
        ;=\,.1                  ;;         BNE  sta1$
                                ;; 
        A=(N)                   ;;         LDA  N
        (2+X)=A                 ;;         STA  2,X
        (3+X)=Y                 ;;         STY  3,X
        X=+                     ;;         INX
        X=+                     ;;         INX
        A=]                     ;;         PLY
        Y=A
        #=!                     ;;         RTS

;; add                          ( n1 n2  --  n1+n2 )
plus                            ;; PLUS:   CLC             
        A=(0+X)                 ;;         LDA  0,X
        A=A+(2+X)               ;;         ADC  2,X
        (2+X)=A                 ;;         STA  2,X
        A=(1+X)                 ;;         LDA  1,X
        A=AC+(3+X)              ;;         ADC  3,X
        (3+X)=A                 ;;         STA  3,X
        X=++                    ;;         POP
        #=!                     ;;         RTS

;; subtract                     ( n1 n2  --  n1-n2 )
minus   C=1                     ;; MINUS:  SEC             
        A=(2+X)                 ;;         LDA  2,X
        A=AC-(0+X)              ;;         SBC  0,X
        (2+X)=A                 ;;         STA  2,X
        A=(3+X)                 ;;         LDA  3,X
        A=AC-(1+X)              ;;         SBC  1,X
        (3+X)=A                 ;;         STA  3,X
        X=++                    ;;         POP
        #=!                     ;;  mi1$:  RTS

over    X=--                    ;; OVER:   DEX2            ; This could be shortened by a few bytes by using PUSH,
        A=(4+X)                 ;;         LDA  4,X        ; but it's used often enough that I think it's worth
        (0+X)=A                 ;;         STA  0,X        ; doing this way.                ( n1 n2 -- n1 n2 n1 )
        A=(5+X)                 ;;         LDA  5,X        ; It makes a copy of the cell below the top of stack.
        (1+X)=A                 ;;         STA  1,X        ; 32 clocks, incl JSR & RTS.
        #=!                     ;;         RTS

dup     X=--                    ;; DUP:    DEX2            ; This could be shortened by a few bytes by using PUSH,
        A=(2+X)                 ;;         LDA  2,X        ; but it's used often enough that I think it's worth
        (0+X)=A                 ;;         STA  0,X        ; doing this way.                       ( n1 -- n1 n1 )
        A=(3+X)                 ;;         LDA  3,X        ; It just DUPlicates the top stack cell.
        (1+X)=A                 ;;         STA  1,X
        #=!                     ;;  durt$: RTS

;; VRAMの指定アドレスに値を書き込む
;; ( addr c -- )
write_to_vram
        A=(3+X)                 ;; アドレス上位
        (PPU_VRAM_ADDR2)=A      ;; 
        A=(2+X)                 ;; アドレス下位
        (PPU_VRAM_ADDR2)=A      ;;  
        A=(0+X)                 ;; 出力したい文字
        (PPU_VRAM_IO)=A         ;; VRAMに書き込み
        X=++++                  ;; スタック解放
        #=!                     ;; 終わり


;; 指定した座標をアドレスに変換、PPUレジスタにセット
;; ( x y -- )
locate
        ;; 1行あたりの文字数をスタックに積む
        X=--
        A=0
        (1+X)=A
        A=SCREEN_WIDTH
        (0+X)=A

        !=star                  ;; y * SCREEN_WIDTH
        !=plus                  ;; + x

        ;; ネームテーブルの先頭アドレスをスタックに積む
        X=--
        A=>VRAM_NAME_TBL
        (1+X)=A
        A=<VRAM_NAME_TBL
        (0+X)=A

        !=plus                  ;; + $2000

        A=(1+X)                 ;; アドレス上位
        (PPU_VRAM_ADDR2)=A      ;; 
        A=(0+X)                 ;; アドレス下位
        (PPU_VRAM_ADDR2)=A      ;;  

        X=++                    ;; スタックを解放

        #=!

;; スタックに積まれた1byteをhex2文字に変換する
;; ( b -- c-hi c-lo )
numhex
        X=--                    ;; スタックを1段深くする        -> [new, new, b, 0]
        A=[2]                   ;; nを読み込む                  A <- b
        A=>>>>                  ;; 上位4bitを下位にずらす
        !=.hexchar              ;; 文字に変換
        [0]=A                   ;; スタックトップに置く
        A=0
        [1]=A
        A=[2]                   ;; もう一度nを取り出す
        !=.hexchar              ;; 文字に変換
        [2]=A                   ;; スタック2段目に置く
        A=0
        [3]=A
        #=!                     ;; 終わり

;; Aレジスタ下位4bitの値を0-Fの文字に変換する
.hexchar
        A=A&$0f                 ;; 上位4bitをマスクする
        ;=A>$A A=A+7
        A=A+$30
        #=!

;; スタックに積まれた1byteをhex2文字で出力する
;; ( b -- )
print_hex
        !=numhex
        !=.print_ch2
        #=!
;; スタックトップと2段目の2文字を表示する
;; ( c2 c1 -- )
.print_ch2
        A=[0]                   ;; 1文字目を出力
        X=++
        (PPU_VRAM_IO)=A
        A=[0]                   ;; 2文字目
        X=++
        (PPU_VRAM_IO)=A
        #=!

;; Aレジスタの値をスタックにプッシュする
;; 上位バイトはゼロ埋め
push_a
        X=--
        [0]=A
        A=0
        [1]=A
        #=!

;; スタックの1段、2段、3段の内容を画面右上に表示する
show_stack
        ;; 座標(26,0)に移動
        A=26
        !=push_a
        A=0
        !=push_a
        !=locate
        A='1'
        (PPU_VRAM_IO)=A
        A=':'
        (PPU_VRAM_IO)=A

        ;; スタックトップを表示
        !=dup
        A=[1]                   ;; hi -> lo
        [0]=A
        !=print_hex
        !=dup
        !=print_hex

        ;; (26,1)に移動
        A=26
        !=push_a
        A=1
        !=push_a
        !=locate
        A='2'
        (PPU_VRAM_IO)=A
        A=':'
        (PPU_VRAM_IO)=A
        ;; 2段目を表示
        !=over
        A=[1]                   ;; hi -> lo
        [0]=A
        !=print_hex
        !=over
        !=print_hex

        ;; (26,2)に移動
        A=26
        !=push_a
        A=2
        !=push_a
        !=locate
        A='3'
        (PPU_VRAM_IO)=A
        A=':'
        (PPU_VRAM_IO)=A
        ;; 3段目を表示
        X=--
        A=[6]                   ;; (pushしたので4段目)をトップにコピー
        [0]=A
        A=[7]
        [1]=A
        !=dup                   ;; さらにコピー
        A=[1]                   ;; hi -> lo
        [0]=A
        !=print_hex
        !=print_hex

        #=!

